(
$data_hip := content[dataset.executor="hip" and dataset.system="MI100_solvers"];
$data_cuda := content[dataset.executor="cuda" and dataset.system="A100_solvers"];

$solvers := $data_cuda.solver~>$keys();

$matrices := [
"thermal2",
"atmosmodj",
"StocF-1465",
"Cube_Coup_dt0",
"CurlCurl_4",
"Bump_2911",
"FullChip",
"rajat31",
"circuit5M",
"nlpkkt160"];

$getColor := function($num_colors, $id) {
  "hsl(" & $floor(360 * $id / $num_colors) & ",40%,55%)"
};

$floor := $string ~> $substringBefore(?, '.') ~> $number;

$get_bandwidth := function($executor) {(
    $hip_bw := 980.516;
    $cuda_bw := 1399.518;
    $executor = "hip" ? $hip_bw : $executor = "cuda" ? $cuda_bw : -1
)};

$memops := function($name, $n, $nnz, $it, $k, $vtype, $itype) {
  (
    $bicgstab := ((15.5 * $n * $vtype + $nnz*$vtype + 2*$nnz*$itype)*$it);
    $cg := ((18 * $n * $vtype + $nnz*$vtype + 2*$nnz*$itype)*$it);
    $cgs := ((14 * $n * $vtype + $nnz*$vtype + 2*$nnz*$itype)*$it);
    $fcg := ((21 * $n * $vtype + $nnz*$vtype + 2*$nnz*$itype)*$it);
    $gmres := (((5/2*$k+21/2+14/$k)*$n * $vtype + (1+1/$k) * ($nnz*$vtype + 2*$nnz*$itype))*$it);
  ($name = "bicgstab" ? $bicgstab :
   $name = "cg" ? $cg :
   $name = "cgs" ? $cgs :
   $name = "fcg" ? $fcg :
   $name = "gmres"? $gmres : -1000000000) / 1000000000
  )
};


$plot := $solvers~>$map(function($v, $i) {{
  "label": $v,
  "data": $data_cuda.{
      "x": problem.name,
      "y": $memops($v, problem.rows, problem.nonzeros, (solver~>$lookup($v)).apply.iterations, 100, 8, 4) / ((solver~>$lookup($v)).apply.time/1000000000)
    },
    "type": "scatter",
    "fill" : false,
    "showLine": false,
    "pointRadius": 5,
    "backgroundColor": $solvers~>$count()~>$getColor($i)
}});

$bw := $matrices~>$map(function($v, $i) {
   $get_bandwidth("cuda")
});

{
  "data": {
    "datasets":
      {
        "label": "STREAM TRIAD",
        "data": $bw,
        "backgroundColor": "rgba(0,0,0,1)",
        "borderColor": "rgba(0,0,0,1)",
        "type": "line",
        "fill": false,
        "showLine": true
      }~>$append($plot)
  },
  "options": {
    "legend": {
      "labels": {
        "fontSize": 20
      }
    },
    "title": {
      "display": true,
      "text": "Bandwidth of selected problems for Ginkgo solvers on A100(CUDA)",
      "fontSize": 20
    },
    "tooltips": {
      "mode": "index",
      "intersect": true
    },
    "scales": {
      "xAxes": [{
        "type": "category",
        "labels": $matrices,
        "ticks": {
          "autoSkip": false,
          "fontSize": 20
        },
        "scaleLabel": {
          "display": true,
          "labelString": "Problem",
          "fontSize": 20
        }
      }],
      "yAxes": [{
          "scaleLabel": {
            "display": true,
            "labelString": "Bandwidth (GB/s)",
            "fontSize": 20
          },
          "offset": true,
          "ticks": {
              "fontSize": 20,
              "beginAtZero" : true
          }
      }]
    }
  }
}
)
